PREFIX schema: <http://schema.org/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX terms: <http://purl.org/dc/terms/>

SELECT DISTINCT ?term ?label
(GROUP_CONCAT(DISTINCT ?subclass; SEPARATOR=", ") AS ?subclasses)
(GROUP_CONCAT(DISTINCT ?superclass; SEPARATOR=", ") AS ?superclasses)
?description ?ontology
WHERE {
    ?term a owl:Class . 
    FILTER(isIri(?term))  # Exclude blank nodes

    OPTIONAL { ?subclass rdfs:subClassOf ?term . FILTER( isIRI(?subclass) && isIRI(?term) )} 
    OPTIONAL { ?term rdfs:subClassOf ?superclass . FILTER( isIRI(?superclass) && isIRI(?term) ) }

    BIND(IRI(REPLACE(STR(?term), "(#|/)[^#/]*$", "")) AS ?ontology)

    OPTIONAL { ?term rdfs:label ?rdfs_label . }
    OPTIONAL { ?term skos:prefLabel ?prefLabel . }
    OPTIONAL { ?term skos:altLabel ?altLabel . }
    OPTIONAL { ?term skos:hiddenLabel ?hiddenLabel . }
  	OPTIONAL { ?term schema:name ?schemaName . }


    BIND(COALESCE(?rdfs_label, ?prefLabel, ?altLabel, ?schemaName, ?hiddenLabel ) AS ?label)

    OPTIONAL { ?term terms:description ?description . }
    OPTIONAL { ?term schema:description ?description . }
  	OPTIONAL { ?term skos:definition ?description . }
 	OPTIONAL { ?term <http://purl.obolibrary.org/obo/IAO_0000115> ?description . }
    OPTIONAL { ?term rdfs:comment ?description . }
}
GROUP BY ?term ?label ?description ?ontology