PREFIX schema: <http://schema.org/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX terms: <http://purl.org/dc/terms/>

SELECT DISTINCT ?term 
    ?label
    ?class 
    (GROUP_CONCAT(DISTINCT ?domain; SEPARATOR=", ") AS ?domains)
    (GROUP_CONCAT(DISTINCT ?range; SEPARATOR=", ") AS ?ranges)
    ?description ?ontology 
WHERE {
    ?term rdf:type ?class .
    FILTER(!isBlank(?term))  # Exclude blank nodes
    FILTER(!isBlank(?class))  # Exclude blank classes
    
    OPTIONAL { ?term rdfs:domain ?domain . FILTER(!isBlank(?domain)) }
    OPTIONAL { ?term rdfs:range ?range . FILTER(!isBlank(?range)) }

    BIND(IRI(REPLACE(STR(?term), "(#|/)[^#/]*$", "")) AS ?ontology)

    OPTIONAL { ?term rdfs:label ?rdfs_label . }
    OPTIONAL { ?term skos:prefLabel ?prefLabel . }
    OPTIONAL { ?term skos:altLabel ?altLabel . }
    OPTIONAL { ?term skos:hiddenLabel ?hiddenLabel . }
    OPTIONAL { ?term schema:name ?schemaName . }


    BIND(COALESCE(?rdfs_label, ?prefLabel, ?altLabel, ?schemaName, ?hiddenLabel ) AS ?label)

    OPTIONAL { ?term terms:description ?description . }
    OPTIONAL { ?term rdfs:comment ?description . }

}
GROUP BY ?term ?label ?class ?description ?ontology